@@ Ambiance object for ___ MUSH
@@
@@ Add little bits of flavor to your daily life!
@@
@@ by Violeta Nascha

@set #99=INHERIT
@set #99=SAFE
@set #99=!NO_COMMAND

@@ Adds the room that you're in to the 'master ambiance list', from which the object draws what rooms to trigger the ambiance messages.

&ADDAMBIENCE #99=$+ambiance/add:@switch [owner(loc(%#))]=%#,{&room_list me=[setunion([v(room_list)],[loc(%#)])];@pemit %#=<AMBIANCE> Ambiant messages enabled in this room.},{@pemit %#=[ufun(#147/error-function)]}

@@ Removes the room from the master ambiance list.

&REMAMBIENCE #99=$+ambiance/rem:@switch [owner(loc(%#))]=%#,{&room_list me=[setdiff([v(room_list)],[loc(%#)])];@pemit %#=<AMBIANCE> Ambiant messages disabled in this room.},{@pemit %#=[ufun(#147/error-function)]}

@@ Makes a player 'deaf' to the ambiance messages.

&AMBOFF #99=$+ambiance/off:&deaf_players me=[setunion(v(deaf_players),%#)];@pemit %#=You tune yourself out from the comings and goings of the world around you\, and focus only on what you're interested in...

@@ Makes a player 'aware' of the ambiance messages.

&AMBON #99=$+ambiance/on:&deaf_players me=[setdiff(v(deaf_players),%#)];@pemit %#=You can now hear ambiance messages. Type 'ambiance off' to turn it off.

@@ Main code for when the ambiance message triggers.

&EMITTER #99=@dolist [v(room_list)]=@pemit/list [setdiff([lcon(##)],[v(deaf_players)])]=[get_eval(##/ambiance[add([rand([words([lattr(##/ambiance*)])])],1)])]

@@ Next part, loops back into main code.

&LOOPER #99=@wait [add(300,[rand(601)])]={@trigger #99/emitter;@trigger #99/looper}



